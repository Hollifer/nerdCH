#
# Ermagerd individual players' chat or just everyone!
#

*:/ermagerd [$player = ''] = >>>
        if(!has_permission("nerdch.ermagerd")) {
                die("You do not have permission to use ermagerd.")
        }

	@pname = to_lower($player);
	# Print usage
	if(@pname == '') {
		die(color(RED) . 'USAGE: /ermagerd (PLAYER | * | -list)');
	}
	@ermagerd_players = import('ermagerd_players')

	# Handle -list option
	if (@pname == '-list') {
		foreach(@key: @value in @ermagerd_players) {
			msg(color(GREEN) . @key)
		}
		die()
	}

	# Ermagerd
	if(array_index_exists(@ermagerd_players, @pname)) {
		array_remove(@ermagerd_players, @pname);
		msg(color(GREEN) . 'Unermagerd ' . @pname);
	} else {
		# Only ermagerd online players
		if(@pname == '*') {
			array_set(@ermagerd_players, @pname, 1);
			msg(color(GREEN) . 'Ermagerd everyone!');
		} else if (ponline(@pname)) {
			array_set(@ermagerd_players, @pname, 1);
			msg(color(GREEN) . 'Ermagerd ' . @pname);
		} else {
			die(color(RED) . 'Unknown player: ' . @pname);
		}
	}
<<<

